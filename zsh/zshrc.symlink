# Shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/dev/dotfiles

# Your project folder that we can `c [tab]` to
export PROJECTS=~/dev

# Use .localrc for SUPER SECRET STUFF that you don't want in your public,
# versioned repo.
if [[ -a ~/.localrc ]]; then
  source ~/.localrc
fi

# All of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# Load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source $file
done

# Load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source $file
done

# Initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# Load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source $file
done

unset config_files
